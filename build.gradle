buildscript {

	repositories {
		maven {
			credentials {
				username readRepoUsername
				password readRepoPassword
			}
			url readRepoUrl
		}
	}
	dependencies {
		classpath 'com.identifix:identifix-project:+'
		classpath "io.spring.gradle:dependency-management-plugin:${springDependencyManagementVersion}"
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
	}
}

plugins {
	id 'com.palantir.docker' version '0.22.1'
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.identifix.identifix-project'

group = 'com.identifix'
sourceCompatibility = 1.8

mainClassName = 'com.identifix.contentlabelingservice.ContentLabelingServiceApplication'

repositories.addAll buildscript.repositories

dependencyManagement {
	imports {
		mavenBom "io.pivotal.spring.cloud:spring-cloud-services-dependencies:${springCloudServicesVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencies {
	implementation 'org.eclipse.jgit:org.eclipse.jgit'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation "com.github.ben-manes.caffeine:caffeine:${caffeineVersion}"
	implementation "org.springframework.boot:spring-boot-starter-aop:${springBootVersion}"
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation "io.springfox:springfox-swagger2:${springfoxSwaggerVersion}"
	implementation "io.springfox:springfox-swagger-ui:${springfoxSwaggerVersion}"
	implementation 'org.codehaus.groovy:groovy'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springframework.cloud:spring-cloud-commons'
	implementation 'org.springframework.cloud:spring-cloud-context'
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'com.identifix:identifix-project:+'
	implementation "com.identifix:crawler-mq-utils:${identifixCrawlerMqUtilsVersion}"
	implementation 'org.jsoup:jsoup:1.11.3'
	compile ("com.identifix:kraken-client:${identifixKrakenClientVersion}") {
		exclude group: 'org.slf4j', module: 'slf4j-simple'
	}
	implementation "com.identifix:html-toolbox:${identifixHtmlToolBoxVersion}"
	implementation "com.rabbitmq:amqp-client:${rabbitAmqpClientVersion}"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation "org.spockframework:spock-core:${spockVersion}"
	testImplementation "org.spockframework:spock-spring:${spockVersion}"
}

docker {
	name "${project.group}/${bootJar.baseName}"             // This will be the name of the docker image when running locally
	copySpec.from("build/libs").into("libs")                // copy the libs folder to build/docker/libs where the docker build can access it
	buildArgs(['JAR_FILE': "libs/${bootJar.archiveName}"])  // tell docker which jar to use
}
